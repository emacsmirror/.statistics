:PREAMBLE:
#+title: Epkgs: Issues
#+setupfile: .orgconfig
#+name: setup
#+begin_src emacs-lisp :results silent
  (require 'emir)
#+end_src
[[https://emacsmirror.net/stats][up]]
:END:
* Features provided by multiple packages
#+name: conflict
#+begin_src emacs-lisp
  (let (alist)
    (dolist (name (epkgs 'name))
      (dolist (feature (epkg-sql [:select feature :from provided
                                  :where (and (= package $s1)
                                              (isnull drop))]
                                 name))
        (setq feature (car feature))
        (if-let ((elt (assq feature alist)))
            (push name (cdr elt))
          (cl-pushnew (list feature name) alist))))
    (epkg-with-org-header ("Feature" "Package" "Shelved")
      (mapcar (pcase-lambda (`(,feature ,package))
                (list feature package
                      (epkg-shelved-package-p (epkg package))))
              (mapcan (pcase-lambda (`(,feature . ,providers))
                        (and (> (length providers) 1)
                             (list (list feature providers))
                             (mapcar (lambda (elt) (list feature elt))
                                     (sort providers #'string<))))
                      (cl-sort alist #'string< :key #'car)))))
#+end_src
#+results: conflict
| Feature (2)      | Package          | Shelved |
|------------------+------------------+---------|
| el-mock          | el-mock          |         |
| el-mock          | rgb              |         |
| ert-expectations | ert-expectations |         |
| ert-expectations | rgb              |         |

- rgb: https://gitlab.com/cwpitts/rgb.el/-/issues/1

* Packages with unsatisfied dependencies
#+name: unsatisfied
#+begin_src emacs-lisp
  (epkg-with-org-header ("Package" "Class" "Feature" "Provider")
    (mapcan
     (pcase-lambda (`(,name ,class))
       (let (alist)
         (pcase-dolist (`(,feature)
                        (epkg-sql [:select feature :from required
                                   :where (and (= package $s1)
                                               (isnull drop))]
                                  name))
           (if-let ((provider (epkg-provided-by feature)))
               (when (epkg-sql [:select class :from packages
                                :where (and (= name $s1)
                                            (= class 'shelved))]
                               provider)
                 (push (list feature provider) alist))
             (push (list feature nil) alist)))
         (mapcar (pcase-lambda (`(,feature ,provider))
                   (list name class feature provider))
                 (cl-sort alist #'string< :key #'car))))
     (epkgs [name class] 'epkg-mirrored-package--eieio-childp)))
#+end_src
#+results: unsatisfied
| Package (6)     | Class  | Feature        | Provider |
|-----------------+--------+----------------+----------|
| auctex-cluttex  | github | tex-buf        |          |
| auctex-latexmk  | github | tex-buf        |          |
| jabber-otr      | github | jabber-chat    |          |
| jabber-otr      | github | jabber-history |          |
| jabber-otr      | github | jabber-muc     |          |
| latex-extra     | github | tex-buf        |          |
| sage-shell-mode | github | tex-buf        |          |
| xenops          | github | tex-buf        |          |

- jabber-otr: https://github.com/melpa/melpa/pull/8055
- tex-buf
  - https://github.com/tsuu32/auctex-cluttex/issues/7
  - https://github.com/tom-tan/auctex-latexmk/issues/44
  - https://github.com/Malabarba/latex-extra/issues/35
  - https://github.com/sagemath/sage-shell-mode/issues/63
  - https://github.com/jhpalmieri/ultratex/issues/1
  - https://github.com/dandavison/xenops/issues/48

* Packages whose repositories have been archived
Don't forget to update ~emir--archived-packages~ occasionally.
#+name: archived
#+begin_src emacs-lisp
  (epkg-with-org-header ("Package" "Repository" "Melpa" "Reverse Dependencies")
    (cl-sort
     (-keep (lambda (name)
              (and (member name emir--archived-packages)
                   (not (assoc name emir-kept-packages))
                   (list name
                         (epkg-org-link name)
                         ;; (and (melpa-get name) "✓")
                         (and (melpa-get name) (oref (epkg name) downloads))
                         (mapconcat (pcase-lambda (`(,name . ,_features)) name)
                                    (epkg-reverse-dependencies name) " "))))
            (epkgs 'name 'epkg-mirrored-package--eieio-childp))
     #'string< :key #'car))
#+end_src
#+results: archived

* Github repositories that were moved or removed
Don't forget to update ~emir--moved-packages~ occasionally.
#+name: github-moved
#+begin_src emacs-lisp
  (epkg-with-org-header ("Package" "Old repository" "New repository" "M")
    (mapcar (pcase-lambda (`(,name ,old ,new))
              (and (epkg name)
                   (list name old new
                         ;;(and (melpa-get name) (oref (epkg name) downloads))))
                         (and (melpa-get name) "✓"))))
            (cl-sort (copy-sequence emir--moved-packages)
                     #'string< :key #'cadr)))
#+end_src
#+results: github-moved

* Missing library summary
#+name: missing-summary
#+begin_src emacs-lisp
  (epkg-with-org-header ("Package" "Class" "M")
    (mapcar (pcase-lambda (`(,name ,class))
              (list name class
                    (and (melpa-get name) "✓")))
            (epkg-sql [:select [name class]
                       :from packages
                       ;;:where (isnull commentary)
                       :where (and (isnull summary)
                                   (not (= class 'builtin))
                                   (not (= class 'shelved)))
                       :order-by [(asc name)]])))
#+end_src
#+results: missing-summary
| Package (4)       | Class   | M |
|-------------------+---------+---|
| arc-dark-theme    | github  | ✓ |
| evil-escape       | github  | ✓ |
| icons-in-terminal | github  |   |
| zeitgeist         | subtree |   |

- arc-dark-theme: https://github.com/cfraz89/arc-dark-theme/pull/2
- evil-escape: https://github.com/syl20bnr/evil-escape/pull/98
- icons-in-terminal: https://github.com/sebastiencs/icons-in-terminal/pull/40
- jabber: https://github.com/legoscia/emacs-jabber/pull/38
- zeitgeist: https://gitlab.freedesktop.org/zeitgeist/zeitgeist-datasources/-/merge_requests/1
