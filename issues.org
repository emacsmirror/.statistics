:PREAMBLE:
#+TITLE: Epkgs: Issues
#+SETUPFILE: .orgconfig
#+NAME: setup
#+BEGIN_SRC emacs-lisp :results silent
  (require 'emir)
#+END_SRC
[[https://emacsmirror.net/stats][up]]
:END:
* Features provided by multiple packages
#+NAME: conflict
#+BEGIN_SRC emacs-lisp
  (let (alist)
    (dolist (name (epkgs 'name))
      (dolist (feature (epkg-sql [:select feature :from provided
                                  :where (and (= package $s1)
                                              (isnull drop))]
                                 name))
        (setq feature (car feature))
        (if-let ((elt (assq feature alist)))
            (push name (cdr elt))
          (cl-pushnew (list feature name) alist))))
    (epkg-with-org-header ("Feature" "Package" "Shelved")
      (mapcar (pcase-lambda (`(,feature ,package))
                (list feature package
                      (epkg-shelved-package-p (epkg package))))
              (mapcan (pcase-lambda (`(,feature . ,providers))
                        (and (> (length providers) 1)
                             (list (list feature providers))
                             (mapcar (lambda (elt) (list feature elt))
                                     (sort providers #'string<))))
                      (cl-sort alist #'string< :key #'car)))))
#+END_SRC
#+RESULTS: conflict

* Packages with unsatisfied dependencies
#+NAME: unsatisfied
#+BEGIN_SRC emacs-lisp
  (epkg-with-org-header ("Package" "Class" "Feature" "Provider")
    (mapcan
     (pcase-lambda (`(,name ,class))
       (let (alist)
         (pcase-dolist (`(,feature)
                        (epkg-sql [:select feature :from required
                                   :where (and (= package $s1)
                                               (isnull drop))]
                                  name))
           (if-let ((provider (epkg-provided-by feature)))
               (when (epkg-sql [:select class :from packages
                                :where (and (= name $s1)
                                            (= class 'shelved))]
                               provider)
                 (push (list feature provider) alist))
             (push (list feature nil) alist)))
         (mapcar (pcase-lambda (`(,feature ,provider))
                   (list name class feature provider))
                 (cl-sort alist #'string< :key #'car))))
     (epkgs [name class] 'epkg-mirrored-package--eieio-childp)))
#+END_SRC
#+RESULTS: unsatisfied

* Packages whose repositories have been archived
Don't forget to update ~emir--archived-packages~ occasionally.
#+NAME: archived
#+BEGIN_SRC emacs-lisp
  (epkg-with-org-header ("Package" "Repository" "M" "Reverse Dependencies")
    (cl-sort
     (-keep (lambda (name)
              (and (member name emir--archived-packages)
                   (not (assoc name emir-kept-packages))
                   (list name
                         (epkg-org-link name)
                         ;; (and (melpa-get name) "✓")
                         (and (melpa-get name) (oref (epkg name) downloads))
                         (mapconcat (pcase-lambda (`(,name . ,_features)) name)
                                    (epkg-reverse-dependencies name) " "))))
            (epkgs 'name 'epkg-mirrored-package--eieio-childp))
     #'string< :key #'car))
#+END_SRC
#+RESULTS: archived

* Github repositories that were moved or removed
Don't forget to update ~emir--moved-packages~ occasionally.
#+NAME: github-moved
#+BEGIN_SRC emacs-lisp
  (epkg-with-org-header ("Package" "Old repository" "New repository" "M")
    (mapcar (pcase-lambda (`(,name ,old ,new))
              (and (epkg name)
                   (list name old new
                         ;;(and (melpa-get name) (oref (epkg name) downloads))))
                         (and (melpa-get name) "✓"))))
            (cl-sort (copy-sequence emir--moved-packages)
                     #'string< :key #'cadr)))
#+END_SRC
#+RESULTS: github-moved
| Package (24)         | Old repository                   | New repository | M |
|----------------------+----------------------------------+----------------+---|
| amread-mode          | stardiviner/amread-mode          |                | ✓ |
| arduino-mode         | stardiviner/arduino-mode         |                | ✓ |
| company-nginx        | stardiviner/company-nginx        |                | ✓ |
| edebug-inline-result | stardiviner/edebug-inline-result |                | ✓ |
| eldoc-overlay        | stardiviner/eldoc-overlay        |                | ✓ |
| rainbow-fart         | stardiviner/emacs-rainbow-fart   |                | ✓ |
| goldendict           | stardiviner/goldendict.el        |                | ✓ |
| inline-docs          | stardiviner/inline-docs.el       |                | ✓ |
| kiwix                | stardiviner/kiwix.el             |                | ✓ |
| major-mode-icons     | stardiviner/major-mode-icons     |                | ✓ |
| mu4e-marker-icons    | stardiviner/mu4e-marker-icons    |                | ✓ |
| ob-php               | stardiviner/ob-php               |                | ✓ |
| ob-redis             | stardiviner/ob-redis             |                | ✓ |
| ob-sclang            | stardiviner/ob-sclang            |                |   |
| ob-smiles            | stardiviner/ob-smiles            |                | ✓ |
| ob-spice             | stardiviner/ob-spice             |                | ✓ |
| org-kindle           | stardiviner/org-kindle           |                | ✓ |
| org-link-beautify    | stardiviner/org-link-beautify    |                | ✓ |
| org-seek             | stardiviner/org-seek.el          |                | ✓ |
| org-tag-beautify     | stardiviner/org-tag-beautify     |                | ✓ |
| proxy-mode           | stardiviner/proxy-mode           |                | ✓ |
| sdcv                 | stardiviner/sdcv.el              |                | ✓ |
| smiles-mode          | stardiviner/smiles-mode          |                | ✓ |
| spice-mode           | stardiviner/spice-mode           |                | ✓ |

- stardiviner: https://github.com/melpa/melpa/issues/7872

* Missing library summary
#+NAME: missing-summary
#+BEGIN_SRC emacs-lisp
  (epkg-with-org-header ("Package" "Class" "M")
    (mapcar (pcase-lambda (`(,name ,class))
              (list name class
                    (and (melpa-get name) "✓")))
            (epkg-sql [:select [name class]
                       :from packages
                       ;;:where (isnull commentary)
                       :where (and (isnull summary)
                                   (not (= class 'builtin))
                                   (not (= class 'shelved)))
                       :order-by [(asc name)]])))
#+END_SRC
#+RESULTS: missing-summary
| Package (5)       | Class   | M |
|-------------------+---------+---|
| arc-dark-theme    | github  | ✓ |
| evil-escape       | github  | ✓ |
| icons-in-terminal | github  |   |
| jabber            | github  | ✓ |
| zeitgeist         | subtree |   |

- arc-dark-theme: https://github.com/cfraz89/arc-dark-theme/pull/2
- evil-escape: https://github.com/syl20bnr/evil-escape/pull/98
- icons-in-terminal: https://github.com/sebastiencs/icons-in-terminal/pull/40
- jabber: https://github.com/legoscia/emacs-jabber/pull/38
- zeitgeist: https://gitlab.freedesktop.org/zeitgeist/zeitgeist-datasources/-/merge_requests/1
